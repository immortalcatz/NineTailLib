buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

repositories {
    maven {
        name "CB's Maven"
        url "http://chickenbones.net/maven"
    }
    maven {
        name "Project Lombok"
        url "https://projectlombok.org/mavenrepo"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

version = "1.11.2-1.5.124"
group = "keri.ninetaillib"
archivesBaseName = "NineTailLib"

sourceCompatibility = targetCompatibility = "1.8" 
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.11.2-13.20.0.2300"
    runDir = "run"
    mappings = "snapshot_20170524"
}

dependencies {
    deobfCompile "codechicken:CodeChickenLib:1.11.2-2.7.0.268:deobf"
    compile "org.projectlombok:lombok:1.16.16"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def commonManifest = {
    attributes 'FMLCorePluginContainsFMLMod': 'true'
    attributes 'FMLCorePlugin': 'keri.ninetaillib.mod.core.NineTailLibCore'
}

jar {
    classifier = 'universal'
    manifest commonManifest
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest commonManifest
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives srcJar
}

task("uploadJars", dependsOn: "reobf") {
    description = "uploads JARs"
    if (System.getenv("local_maven") != null) {
        apply plugin: 'maven'
        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: "file://" + System.getenv("local_maven"))
                    pom {
                        groupId = project.group
                        version = project.version
                        artifactId = project.archivesBaseName
                        project {
                            name project.archivesBaseName
                            packaging 'jar'
                            description 'NineTailLib'
                            url 'http://github.com/TeamMD5/NineTailLib'
                            scm {
                                url 'http://github.com/TeamMD5/NineTailLib'
                                connection 'scm:git:git@github.com:TeamMD5/NineTailLib/NineTailLib.git'
                                developerConnection 'scm:git:git@github.com:TeamMD5/NineTailLib/NineTailLib.git'
                            }
                            issueManagement {
                                system 'github'
                                url 'http://github.com/TeamMD5/NineTailLib/issues'
                            }
                            licenses {
                                license {
                                    name 'GNUv3'
                                    url 'http://github.com/TeamMD5/NineTailLib/blob/master/LICENSE'
                                    distribution 'repo'
                                }
                            }
                            developers {
                                developer {
                                    id 'KitsuneAlex'
                                    name 'KitsuneAlex'
                                    roles { role 'developer' }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
